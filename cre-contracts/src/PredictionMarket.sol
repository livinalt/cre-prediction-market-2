// SPDX-License-Identifier: MIT
pragma solidity 0.8.24;

import {ReceiverTemplate} from "./interfaces/ReceiverTemplate.sol";

contract PredictionMarket is ReceiverTemplate {
    error MarketDoesNotExist();
    error MarketAlreadySettled();
    error MarketNotSettled();
    error AlreadyPredicted();
    error InvalidAmount();
    error NothingToClaim();
    error AlreadyClaimed();
    error TransferFailed();

    event MarketCreated(uint256 indexed marketId, string question, string descriptionCID, address creator);
    event PredictionMade(uint256 indexed marketId, address indexed predictor, Prediction prediction, uint256 amount);
    event SettlementRequested(uint256 indexed marketId, string question);
    event MarketSettled(uint256 indexed marketId, Prediction outcome, uint16 confidence);
    event WinningsClaimed(uint256 indexed marketId, address indexed claimer, uint256 amount);

    enum Prediction { Yes, No }

    struct Market {
        address creator;
        uint48 createdAt;
        uint48 settledAt;
        bool settled;
        uint16 confidence;
        Prediction outcome;
        uint256 totalYesPool;
        uint256 totalNoPool;
        string question;
        string descriptionCID; // IPFS CID — empty string if no description provided
    }

    struct UserPrediction {
        uint256 amount;
        Prediction prediction;
        bool claimed;
    }

    uint256 internal nextMarketId;
    mapping(uint256 => Market) internal markets;
    mapping(uint256 => mapping(address => UserPrediction)) internal predictions;

    constructor(address _forwarderAddress) ReceiverTemplate(_forwarderAddress) {}

    // descriptionCID is optional — pass empty string "" if no description
    function createMarket(string memory question, string memory descriptionCID) public returns (uint256 marketId) {
        marketId = nextMarketId++;
        markets[marketId] = Market({
            creator:        msg.sender,
            createdAt:      uint48(block.timestamp),
            settledAt:      0,
            settled:        false,
            confidence:     0,
            outcome:        Prediction.Yes,
            totalYesPool:   0,
            totalNoPool:    0,
            question:       question,
            descriptionCID: descriptionCID
        });
        emit MarketCreated(marketId, question, descriptionCID, msg.sender);
    }

    function predict(uint256 marketId, Prediction prediction) external payable {
        Market memory m = markets[marketId];
        if (m.creator == address(0)) revert MarketDoesNotExist();
        if (m.settled) revert MarketAlreadySettled();
        if (msg.value == 0) revert InvalidAmount();
        UserPrediction memory userPred = predictions[marketId][msg.sender];
        if (userPred.amount != 0) revert AlreadyPredicted();

        predictions[marketId][msg.sender] = UserPrediction({
            amount:     msg.value,
            prediction: prediction,
            claimed:    false
        });

        if (prediction == Prediction.Yes) {
            markets[marketId].totalYesPool += msg.value;
        } else {
            markets[marketId].totalNoPool += msg.value;
        }
        emit PredictionMade(marketId, msg.sender, prediction, msg.value);
    }

    function requestSettlement(uint256 marketId) external {
        Market memory m = markets[marketId];
        if (m.creator == address(0)) revert MarketDoesNotExist();
        if (m.settled) revert MarketAlreadySettled();
        emit SettlementRequested(marketId, m.question);
    }

    function _settleMarket(bytes calldata report) internal {
        (uint256 marketId, Prediction outcome, uint16 confidence) = abi.decode(
            report, (uint256, Prediction, uint16)
        );
        Market memory m = markets[marketId];
        if (m.creator == address(0)) revert MarketDoesNotExist();
        if (m.settled) revert MarketAlreadySettled();

        markets[marketId].settled    = true;
        markets[marketId].confidence = confidence;
        markets[marketId].settledAt  = uint48(block.timestamp);
        markets[marketId].outcome    = outcome;
        emit MarketSettled(marketId, outcome, confidence);
    }

    function _processReport(bytes calldata report) internal override {
        if (report.length > 0 && report[0] == 0x01) {
            _settleMarket(report[1:]);
        } else {
            // HTTP trigger path: abi-encoded (question, descriptionCID)
            // If only question is encoded (legacy), descriptionCID defaults to ""
            if (report.length > 64) {
                (string memory question, string memory descriptionCID) = abi.decode(report, (string, string));
                createMarket(question, descriptionCID);
            } else {
                string memory question = abi.decode(report, (string));
                createMarket(question, "");
            }
        }
    }

    function claim(uint256 marketId) external {
        Market memory m = markets[marketId];
        if (m.creator == address(0)) revert MarketDoesNotExist();
        if (!m.settled) revert MarketNotSettled();

        UserPrediction memory userPred = predictions[marketId][msg.sender];
        if (userPred.amount == 0) revert NothingToClaim();
        if (userPred.claimed) revert AlreadyClaimed();
        if (userPred.prediction != m.outcome) revert NothingToClaim();

        predictions[marketId][msg.sender].claimed = true;
        uint256 totalPool   = m.totalYesPool + m.totalNoPool;
        uint256 winningPool = m.outcome == Prediction.Yes ? m.totalYesPool : m.totalNoPool;
        uint256 payout      = (userPred.amount * totalPool) / winningPool;
        (bool success,)     = msg.sender.call{value: payout}("");
        if (!success) revert TransferFailed();
        emit WinningsClaimed(marketId, msg.sender, payout);
    }

    function getMarket(uint256 marketId) external view returns (Market memory) {
        return markets[marketId];
    }

    function getPrediction(uint256 marketId, address user) external view returns (UserPrediction memory) {
        return predictions[marketId][user];
    }

    function getNextMarketId() external view returns (uint256) {
        return nextMarketId;
    }
}
